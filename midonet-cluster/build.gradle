/*
 * Copyright 2015 Midokura SARL
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'scala'
apply plugin: 'application'
apply from: "${rootProject.projectDir}/fpm.gradle"
apply from: "${rootProject.projectDir}/git.gradle"

jar {
    manifest {
        attributes 'Implementation-Title': 'MidoNet Cluster',
        'Implementation-Version': version
    }
}

repositories {
    maven { url "http://nexus.opendaylight.org/content/repositories/opendaylight.release/" }
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala', 'src/main/java'] }
        java { srcDirs = [] }
    }
    test {
        scala { srcDirs = ['src/test/scala', 'src/test/java'] }
        java { srcDirs = [] }
    }
}

dependencies {
    compile project(':nsdb'),
            project(':midolman'),
            project(':midonet-util'),
            "org.opendaylight.ovsdb:library:1.1.1-Lithium-SR1",
            'org.slf4j:jul-to-slf4j:1.7.12',
            "org.jasypt:jasypt:1.7",
            libraries.metrics,
            libraries.validation,
            libraries.hibernate_validator,
            libraries.jersey_client,
            libraries.jersey_core,
            libraries.jersey_guice,
            libraries.jersey_server,
            libraries.jersey_json,
            libraries.jackson_annotations,
            libraries.jackson_core,
            libraries.jackson_databind,
            libraries.jackson_jaxrs,
            libraries.commons_dbcp2,
            libraries.commons_codec,
            libraries.curator_recipes,
            libraries.curator_framework,
            libraries.guice,
            libraries.jetty_server,
            libraries.jetty_servlet,
            libraries.netty,
            libraries.mysql_jdbc,
            libraries.protobuf,
            libraries.rx,
            libraries.scala,
            libraries.zookeeper

    testCompile project(path: ':nsdb', configuration: 'tests')
    testCompile project(path: ':midolman', configuration: 'tests')
    testCompile project(path: ':midonet-util', configuration: 'tests')
    testCompile libraries.curator_test,
                libraries.grizzly_servlet,
                libraries.hamcrest_lib,
                libraries.httpclient,
                libraries.jersey_grizzly,
                libraries.jersey_test_core,
                libraries.jersey_test_grizzly,
                libraries.mockit,
                libraries.scalatest,
                libraries.sqlite
}

cobertura {
    coverageExcludes = [
            '.*org.midonet.client.*',
            '.*org.midonet.migrator.*',
            // Keystone excluded, this is tested in MDTS
            '.*org.midonet.cluster.auth.keystone.v2_0.*',
            '.*org.midonet.midolman.state.*',
            '.*org.midonet.midolman.state.zkManagers.*',
            '.*org.midonet.cluster.LocalDataClientImpl.*',
            '.*org.midonet.cluster.services.heartbeat.*',
            // Cassandra is tested in Integration tests from another
            // module, so coverage doesn't show up
            '.*org.midonet.cluster.backend.cassandra.*',
            // The TopologySnooopy is a dev tool
            '.*org.midonet.cluster.tools.TopologySnoopy.*',
            '.*org.midonet.cluster.services.topology.client.*',
            '.*org.midonet.cluster.services.topology.TopologyServiceApp.*',
    ]
}

/////////////////////
// PACKAGING TASKS //
/////////////////////

task preparePkg(type: Copy, dependsOn: installShadowApp) {
    from("${buildDir}/installShadow/${project.name}/lib/${project.name}-${midonetVersion}-all.jar") {
        into "/usr/share/midonet-cluster"
    }
    from("conf") {
        into "etc/midonet-cluster"
        include "midonet-cluster-env.sh"
        include "logback.xml"
    }
    from("../nsdb/conf/midonet.conf") {
        into "etc/midonet"
    }
    from("src/deb/init/midonet-cluster.conf") {
        into "etc/init"
    }
    from("src/deb/init/midonet-cluster-start") {
        into "usr/share/midonet-cluster"
    }
    from("src/deb/init/midonet-cluster-env.sh") {
        into "usr/share/midonet-cluster"
    }
    from("src/deb/bin/mn-migrate") {
        into "usr/bin"
    }
    into "${pkgDestDir}"
    doLast {
        new File("${pkgDestDir}/usr/share/midonet-cluster/${project.name}-${midonetVersion}-all.jar")
            .renameTo("${pkgDestDir}/usr/share/midonet-cluster/${project.name}.jar")
    }
}

task rhel7Prepare(type: Copy, dependsOn: installShadowApp) {
    from("src/rhel7/systemd/midonet-cluster.service") {
        into "usr/lib/systemd/system"
    }
    into "${pkgDestDir}"
}

packaging {
    version = project.debVersion
    maintainer = project.maintainer
    vendor = project.vendor
    url = project.url
    description = "MidoNet's control cluster"
    dependencies = ['midonet-tools']
    confFiles = ['/etc/midonet-cluster/logback.xml',
                 '/etc/midonet/midonet.conf',
                 '/etc/midonet-cluster/midonet-cluster-env.sh']
    fpmOpts = ['-a', 'all',
               '--category', 'network',
               '--url', 'http://www.midonet.org',
               '--epoch', '2']
}

preparePkg.dependsOn cleanPkg
debian.dependsOn preparePkg

rpm.dependsOn rhel7Prepare
rhel7Prepare.dependsOn preparePkg

debian.doFirst {
    project.packaging.version = project.debVersion
    project.packaging.fpmOpts = project.packaging.fpmOpts.clone()
    project.packaging.fpmOpts.addAll([
            '-d', 'java8-runtime-headless | java8-runtime',
            '--deb-recommends', 'java8-runtime-headless | java8-runtime',
            '--after-install', "${project.projectDir}/src/deb/control/postinst",
            '--before-install', "${project.projectDir}/src/deb/control/preinst",
            '--after-remove', "${project.projectDir}/src/deb/control/postrm",
            '--before-remove', "${project.projectDir}/src/deb/control/prerm" ])
}

rpm.doFirst {
    project.packaging.version = project.rpmVersion
    project.packaging.iteration = project.rpmRelease
    project.packaging.fpmOpts = project.packaging.fpmOpts.clone()
    project.packaging.fpmOpts.addAll([
            '--after-install', "${project.projectDir}/src/rhel7/control/postinst",
            '--before-install', "${project.projectDir}/src/rhel7/control/preinst",
            '--after-remove', "${project.projectDir}/src/rhel7/control/postrm",
            '--before-remove', "${project.projectDir}/src/rhel7/control/prerm",
            '--depends', 'java-1.8.0-openjdk-headless'])
}

debian.doLast {
    new File("${buildDir}/install/${project.name}").deleteDir()
}
rpm.doLast {
    new File("${buildDir}/install/${project.name}").deleteDir()
}

mainClassName = "org.midonet.cluster.ClusterNode"

/////////////////////
// EXECUTION TASKS //
/////////////////////

// Runs the cluster daemon, locally. use
//    -Pconf=$PATH_TO_MIDONET_CLUSTER_CONF
run {

    // so that it picks up build.gradle
    classpath += files('conf/')

    mainClassName = "org.midonet.cluster.ClusterNode"
    systemProperties System.getProperties()
    if (project.hasProperty("keystore_path")) {
        systemProperty "midonet.keystore_path", keystore_path
    }
    if (project.hasProperty("keystore_password")) {
        systemProperty "midonet.keystore_password", keystore_password
    }
    if (project.hasProperty("conf")) {
        args = [conf]
    }

    jvmArgs += '-Dcom.sun.management.jmxremote.local.only=true'
    jvmArgs += '-Dcom.sun.management.jmxremote.authenticate=false'
    jvmArgs += '-Dcom.sun.management.jmxremote.port=7201'
    jvmArgs += '-Dcom.sun.management.jmxremote.ssl=false'
}

// Runs the Topology API service in stand-alone mode.
// Use -Pconf=<path_to_config_file> to start it pointing at a custom config file.
// Note that the configuration file path is relative to the midonet-cluster
// submodule, i.e., -Pconf=midonet-cluster/conf/<config_file>.
task runTopologyApi(type: JavaExec) {
    description = 'Run the Topology API Service - standalone'
    classpath sourceSets.main.runtimeClasspath
    main = "org.midonet.cluster.services.topology.TopologyApiServiceApp"
    if (project.hasProperty("conf")) {
        args = [conf]
    }
}

// Runs a topology text-based client that dumps updates made to the topology
// Use -Pconf=conf/topology-client.conf to start it up pointing at the sample
// config file in cluster/midonet-cluster/conf (note that the path is relative to
// the midonet-cluster submodule
task runTopologySnoopy(type: JavaExec) {
    description = 'Run the Topology Snoopy client'
    classpath sourceSets.main.runtimeClasspath
    main = "org.midonet.cluster.tools.TopologySnoopyApp"
    if (project.hasProperty("conf")) {
        args = [conf]
    } else {
        args = ["conf/topology-client.conf"]
    }
}
