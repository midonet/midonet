/*
 * Copyright 2014 Midokura SARL
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'application'

jar {
    manifest {
        attributes 'Implementation-Title': 'Vtep Support',
                   'Implementation-Version': version
    }
}

repositories {
    maven { url "http://nexus.opendaylight.org/content/repositories/opendaylight.release/" }
    maven { url "http://nexus.opendaylight.org/content/repositories/public/"  }
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala', 'src/main/java'] }
        java { srcDirs = [] }
    }
    test {
        scala { srcDirs = ['src/test/scala', 'src/test/java'] }
        java { srcDirs = [] }
    }
}

dependencies {
    compile project(':midonet-util'),
            project(':cluster'),
            libraries.rx,
            "org.opendaylight.ovsdb:library:1.0.0-Helium"

    testCompile libraries.mockit,
                libraries.scalatest
}

cobertura {
    coverageSourceDirs << sourceSets.main.java.srcDirs
    coverageSourceDirs << sourceSets.main.scala.srcDirs
}

// Runs the vtep data dump test application
// Use -Pvtep=<ip> [-Pport=<port>] to start it up pointing at the requested vtep
task runVtepDump(type: JavaExec) {
    description = 'Run the Vtep dump application'
    classpath sourceSets.main.runtimeClasspath
    main = "org.midonet.vtep.VtepDumpApp"
    if (project.hasProperty("port")) {
        if (project.hasProperty("vtep")) {
            args = [vtep, port]
        }
    } else {
        if (project.hasProperty("vtep")) {
            args = [vtep, "6632"]
        }
    }
}
