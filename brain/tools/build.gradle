/*
 * Copyright 2015 Midokura SARL
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'scala'
apply plugin: 'application'

jar {
    manifest {
        attributes 'Implementation-Title': 'Midonet Brain Tools',
        'Implementation-Version': version
    }
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala', 'src/main/java'] }
        java { srcDirs = [] }
    }
    test {
        scala { srcDirs = ['src/test/scala', 'src/test/java'] }
        java { srcDirs = [] }
    }
}

dependencies {
    compile project(':cluster'),
            project(':midonet-util'),
            libraries.commons_conf,
            libraries.curator_recipes,
            libraries.guava,
            libraries.guice,
            libraries.protobuf,
            libraries.zookeeper

    runtime libraries.jackson_core,
            libraries.jackson_mapper,
            libraries.scala,
            libraries.slf4j

    testCompile libraries.curator_test,
                libraries.hamcrest_lib,
                libraries.mockit,
                libraries.scalatest
}

// Runs the zookeeper updater for topology data. This tool provides a way
// to populate ZooKeeper with topology objects, for use in testing other
// components. Use -Pconf=conf/topology-zoomupdater.conf to start it up
// pointing a the sample config file in brain/midonet-brain/conf.
// Note that the path is relative to the midonet-brain submodule
task runTopologyZoomUpdater(type: JavaExec) {
    description = 'Run the Topology API Zoom updater app'
    classpath sourceSets.main.runtimeClasspath
    main = "org.midonet.brain.tools.TopologyZoomUpdaterApp"
    if (project.hasProperty("conf")) {
        args = [conf]
    } else {
        args = ["conf/topology-zoomupdater.conf"]
    }
}

cobertura {
    coverageSourceDirs << sourceSets.main.java.srcDirs
    coverageSourceDirs << sourceSets.main.scala.srcDirs
}
