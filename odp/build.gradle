/*
 * Copyright 2015 Midokura SARL
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'scala'

jar {
    manifest {
        attributes 'Implementation-Title': 'OpenVSwitch Datapath client library',
        'Implementation-Version': version
    }
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala', 'src/main/java'] }
        java { srcDirs = [] }
    }
    test {
        scala { srcDirs = ['src/test/scala', 'src/test/java'] }
        java { srcDirs = [] }
    }
    perf {
        scala { srcDirs = ['src/perf/scala', 'src/perf/java'] }
        java { srcDirs = [] }
    }
}

dependencies {
    compile project(":midonet-util"), project(":netlink")

    compile libraries.hamcrest_core, libraries.hamcrest_lib,
            libraries.commons_cli, libraries.commons_exec,
            libraries.jna, libraries.scala, libraries.rx

    testCompile libraries.scalatest
}

cobertura {
    coverageExcludes = ['.*org.midonet.odp.test.*']
}

task ovsTest(type: Exec, dependsOn: testClasses) {
    commandLine 'sudo', 'java', '-Djava.library.path=/lib:/usr/lib'
    commandLine += "-Xbootclasspath/p:${project.jdkBootstrap}"
    commandLine += '-cp'
    commandLine += (sourceSets.main.runtimeClasspath +
                    configurations.runtime).collect { it.toString() }.join(':')
    commandLine += 'org.midonet.odp.test.OvsIntegrationTest'
}

task rtnetlinkTest(type: Exec, dependsOn: testClasses) {
    commandLine 'sudo', 'java', '-Djava.library.path=/lib:/usr/lib'
    commandLine += "-Xbootclasspath/p:${project.jdkBootstrap}"
    commandLine += '-cp'
    commandLine += (sourceSets.main.runtimeClasspath +
                    configurations.runtime).collect { it.toString() }.join(':')
    commandLine += 'org.midonet.odp.test.RtnetlinkIntegrationTest'
}
