project.ext {
    midonetVersion = "1.6-SNAPSHOT"
    vendor = 'Midokura'
    maintainer = "MidoNet Team <info@midokura.com>"
    url = "http://midokura.com"

    jdkBootstrap = "${rootDir}/midonet-jdk-bootstrap/build/classes/main:${System.properties['java.home']}/lib/rt.jar"
    ideaJdkBootstrap = "-Xbootclasspath/p:${rootDir}/idea/out/midonet-jdk-bootstrap"
    scalaLangLevel = 'Scala 2.10.3'

    versions = [akka: "2.2.3", guice: "3.0", hamcrest: "1.3", jackson: "1.9.3",
                java: "1.7",   jersey: "1.10", jetty: "7.6.14.v20131031",
                jsonpath: "0.9.1"]

    libraries = [
        sun_tools: project.files("${System.properties['java.home']}/../lib/tools.jar"),

        scala: "org.scala-lang:scala-library:2.10.3",
        scalatest: "org.scalatest:scalatest_2.10:2.0",
        akka: ["com.typesafe.akka:akka-slf4j_2.10:${versions.akka}",
               "com.typesafe.akka:akka-actor_2.10:${versions.akka}"],
        akkatest: "com.typesafe.akka:akka-testkit_2.10:${versions.akka}",

        cassandra: "com.datastax.cassandra:cassandra-driver-core:2.0.1",
        cassandraunit: dependencies.create("org.cassandraunit:cassandra-unit:2.0.2.1") {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'},

        netty: "io.netty:netty:3.7.0.Final",
        guava: "com.google.guava:guava:15.0",
        slf4j: "org.slf4j:slf4j-api:1.7.7",
        findbugs: "com.google.code.findbugs:jsr305:2.0.0",
        logback: "ch.qos.logback:logback-classic:1.0.1",
        jna: "net.java.dev.jna:jna:3.3.0",
        jsch: 'com.jcraft:jsch:0.1.45',
        collections4: 'org.apache.commons:commons-collections4:4.0',
        commons_codec: "commons-codec:commons-codec:1.2",
        commons_conf: "commons-configuration:commons-configuration:1.8",
        commons_lang: "commons-lang:commons-lang:2.6",
        commons_lang3: "org.apache.commons:commons-lang3:3.3",
        commons_cli: "commons-cli:commons-cli:1.2",
        commons_io: "commons-io:commons-io:2.3",
        guice: ["com.google.inject:guice:${versions.guice}",
                "com.google.inject.extensions:guice-assistedinject:${versions.guice}"],
        guice_servlet: "com.google.inject.extensions:guice-servlet:${versions.guice}",
        guice_multibind: "com.google.inject.extensions:guice-multibindings:${versions.guice}",
        yammer: ["com.yammer.metrics:metrics-core:2.1.2",
                 "com.yammer.metrics:metrics-ganglia:2.1.2"],
        rx: "com.netflix.rxjava:rxjava-core:0.17.6",
        jackson_jaxrs: "org.codehaus.jackson:jackson-jaxrs:${versions.jackson}",
        jackson_xc: "org.codehaus.jackson:jackson-xc:${versions.jackson}",
        jackson_core: "org.codehaus.jackson:jackson-core-asl:${versions.jackson}",
        jackson_mapper: "org.codehaus.jackson:jackson-mapper-asl:${versions.jackson}",
        jackson_scala: dependencies.create("com.fasterxml:jackson-module-scala:${versions.jackson}") {
            exclude group: 'junit', module: 'junit'
            exclude group: 'org.scalatest', module: 'scalatest_2.9.1'
            exclude group: 'com.google.guava', module: 'guava'},
        jsonpath: "com.jayway.jsonpath:json-path:${versions.jsonpath}",
        jsonpath_assert: "com.jayway.jsonpath:json-path-assert:${versions.jsonpath}",
        test_frameworks: ["junit:junit:4.11",
                          "org.powermock:powermock-module-junit4:1.4.11",
                          "org.powermock:powermock-api-mockito:1.4.11",
                          "org.mockito:mockito-all:1.9.0",
                          "pl.pragmatists:JUnitParams:1.0.2",
                          "nl.jqno.equalsverifier:equalsverifier:1.4.1"],

        hamcrest: "org.hamcrest:hamcrest-all:${versions.hamcrest}",
        hamcrest_core: "org.hamcrest:hamcrest-core:${versions.hamcrest}",
        hamcrest_lib: "org.hamcrest:hamcrest-library:${versions.hamcrest}",
        hamcrest_integration: "org.hamcrest:hamcrest-integration:${versions.hamcrest}",

        zookeeper: dependencies.create("org.apache.zookeeper:zookeeper:3.4.6") {
            exclude group: 'jline', module: 'jline'
            exclude group: 'javax.jms', module: 'javax.jms'
            exclude group: 'com.sun.jdmk', module: 'jmxtools'
            exclude group: 'com.sun.jmx', module: 'jmxri'
            exclude group: 'io.netty', module: 'netty'
            exclude group: 'log4j', module: 'log4j'
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'},
        curator_recipes: "org.apache.curator:curator-recipes:2.6.0",
        curator_test: "org.apache.curator:curator-test:2.6.0",

        persistence: "javax.persistence:persistence-api:1.0",
        jsr250: "javax.annotation:jsr250-api:1.0",
        jsr311: "javax.ws.rs:jsr311-api:1.1",
        validation: "javax.validation:validation-api:1.0.0.GA",
        servlet: "javax.servlet:servlet-api:2.5",

        jetty: dependencies.create("org.mortbay.jetty:jetty:6.1.26") {
            exclude group: 'org.mortbay.jetty', module: 'servlet-api'},
        jetty_server: dependencies.create("org.eclipse.jetty:jetty-server:${versions.jetty}") {
            exclude group: "org.eclipse.jetty:orbit:javax.servlet"
        },
        jetty_websocket: "org.eclipse.jetty:jetty-websocket:${versions.jetty}",
        jetty_deploy: "org.eclipse.jetty:jetty-deploy:${versions.jetty}",
        jminix: "org.jminix:jminix:1.1.0",

        hibernate_validator: "org.hibernate:hibernate-validator:4.3.0.Final",

        grizzly_servlet: "org.glassfish.grizzly:grizzly-http-servlet:2.1.2",
        jersey_core: "com.sun.jersey:jersey-core:${versions.jersey}",
        jersey_servlet: "com.sun.jersey:jersey-servlet:${versions.jersey}",
        jersey_server: "com.sun.jersey:jersey-server:${versions.jersey}",
        jersey_client: "com.sun.jersey:jersey-client:${versions.jersey}",
        jersey_grizzly: "com.sun.jersey:jersey-grizzly2:${versions.jersey}",
        jersey_guice: "com.sun.jersey.contribs:jersey-guice:${versions.jersey}",
        jersey_json: dependencies.create("com.sun.jersey:jersey-json:${versions.jersey}") {
            exclude group: 'com.sun.xml.bind', module: 'jaxb-impl'},
        jersey_test_grizzly: dependencies.create(
            "com.sun.jersey.jersey-test-framework:jersey-test-framework-grizzly2:${versions.jersey}") {
                exclude group: 'junit', module: 'junit'},
        jersey_test_core: dependencies.create(
            "com.sun.jersey.jersey-test-framework:jersey-test-framework-core:${versions.jersey}") {
                exclude group: 'junit', module: 'junit'},

        truelicense: "net.java.truelicense:truelicense-json:2.3.3",

        vmware_midoapi: [ "com.vmware:vijava:5.1", "dom4j:dom4j:1.6.1" ],
        jcabi: "com.jcabi:jcabi-aspects:0.15.1",
        aspectj: "org.aspectj:aspectjrt:1.8.0"
    ]

    timestamp = new Date().format("YYYYMMddHHmm")
    debVersion = midonetVersion.replaceAll(/SNAPSHOT$/, "${timestamp}").
                                replaceAll(/-/, "~")
    rpmVersion = midonetVersion.replaceAll(/-.*$/, "")
    rpmRelease = midonetVersion.replaceAll(/^[^-]+$/, "1.0").
                                replaceAll(/^.*SNAPSHOT$/, "0.0.${timestamp}").
                                replaceAll(/^.*-(.*)$/, "0.1.\$1")

}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.9.0-M3'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
        classpath 'com.andrewkroh.gradle:gradle-protobuf-plugin:0.3.0'
    }
}

allprojects {
    apply plugin: 'idea'

    plugins.withType(ScalaPlugin).whenPluginAdded {
        dependencies { compile libraries.scala }

        tasks.withType(ScalaCompile) {
            scalaCompileOptions.fork = true
            scalaCompileOptions.useAnt = false
            scalaCompileOptions.deprecation = true
            scalaCompileOptions.unchecked = true
            scalaCompileOptions.additionalParameters = ["-language:_"]
        }
        idea.module.iml.withXml { p -> addScalaLangLevelToIdea(p) }
    }
    idea.module.iml.withXml { p -> reorderIdeaJdkBootstrapDependency(p) }
    idea.module.iml.generateTo = file("${rootDir}/idea")
}

idea.project {
    languageLevel = versions.java
    outputFile = file("${rootDir}/idea/midonet.ipr")
}

configure(subprojects.findAll {!it.name.contains('ovsdb')}) {
    apply plugin: 'java'
    apply plugin: 'cobertura'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply from: "${rootProject.projectDir}/git.gradle"

    sourceCompatibility = versions.java

    version = midonetVersion
    group = 'org.midonet'

    configurations {
        provided
        perfCompile.extendsFrom compile
        perfRuntime.extendsFrom runtime
    }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        perf.compileClasspath += sourceSets.main.compileClasspath
        perf.compileClasspath += sourceSets.test.compileClasspath
        perf.compileClasspath += sourceSets.main.output
        perf.compileClasspath += sourceSets.test.output
        perf.runtimeClasspath += sourceSets.main.runtimeClasspath
        perf.runtimeClasspath += sourceSets.test.runtimeClasspath
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://jsch.sf.net/maven2/" }
        maven { url "http://maven.restlet.org" }
    }

    tasks.build {
        dependsOn(perfClasses)
    }

    dependencies {
        compile libraries.slf4j
        compile libraries.guava
        compile libraries.findbugs
        testCompile libraries.test_frameworks
        runtime libraries.logback
        perfCompile 'org.openjdk.jmh:jmh-core:0.9'
        perfCompile 'org.openjdk.jmh:jmh-generator-bytecode:0.9'
    }

    idea.module {
        scopes.PROVIDED.plus += configurations.perfCompile
        scopes.PROVIDED.plus += configurations.provided
        inheritOutputDirs = false
        outputDir = file("${rootDir}/idea/out/${project.name}")
        testOutputDir = file("${rootDir}/idea/test/${project.name}")
    }

    // The JMH annotation processor doesn't support Scala (yet?), so
    // we use the bytecode generator, which requires these two tasks.
    def benchOutput = sourceSets.perf.output.classesDir.absolutePath + '/generated-sources'

    task cleanPerf(type: Delete) {
        delete benchOutput
    }

    task generateBenchmarks(type: JavaExec) {
        main = 'org.openjdk.jmh.generators.bytecode.JmhBytecodeGenerator'
        classpath = sourceSets.perf.compileClasspath
        args += sourceSets.perf.output.classesDir // compiled-bytecode-dir
        args += benchOutput  // output-source-dir
        args += benchOutput // output-resource-dir
        args += 'asm'

        dependsOn(perfClasses, cleanPerf)
    }

    task compileBenchmarks(type: JavaCompile) {
        classpath = sourceSets.perf.runtimeClasspath
        source = benchOutput
        destinationDir = file(benchOutput)

        dependsOn(generateBenchmarks)
    }

    task benchmarks(type: JavaExec) {
        main = 'org.openjdk.jmh.Main'
        classpath = sourceSets.perf.runtimeClasspath + files(benchOutput)
        maxHeapSize = "1024m"
        description 'Executes the specified benchmarks. By default runs all. ' +
                    'Example command: ./gradlew :midonet-util:benchmarks \'-Pjmh=.*Statistical.*\''

        if (project.hasProperty('jmh')) {
            args(jmh.split(' '))
        }

        dependsOn(compileBenchmarks)
    }

    shadowJar {
        archiveName = "benchmarks.jar"
        from sourceSets.perf.runtimeClasspath + files(benchOutput)
        from configurations.perfRuntime

        appendManifest {
            attributes 'Main-Class': 'org.openjdk.jmh.Main'
        }

        dependsOn(compileBenchmarks)
    }

    cobertura {
        rootProject.subprojects.each {
            coverageDirs << file("${it.name}/build/classes/main")
        }
    }
}

configure(subprojects.findAll {it.name.contains('ovsdb')}) {
    group = 'org.midonet'
    version = '0.5.0-1-MIDO'

    apply plugin: 'java'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile "com.google.guava:guava:14.0.1",
                "com.google.code.gson:gson:2.1",
                "com.netflix.rxjava:rxjava-core:0.17.6",
                "org.opendaylight.controller:clustering.services:0.5.0",
                "org.opendaylight.controller:sal.connection:0.1.1",
                "org.opendaylight.controller:sal.networkconfiguration:0.0.2",
                "javax.portlet:portlet-api:2.0",
                "org.apache.httpcomponents:httpcore-nio:4.2.1",
                "io.netty:netty-all:4.0.10.Final",
                "commons-collections:commons-collections:1.0",
                "com.fasterxml.jackson.core:jackson-databind:2.3.2",
                "com.fasterxml.jackson.core:jackson-core:2.3.2",
                "com.fasterxml.jackson.core:jackson-annotations:2.3.2",
                "equinoxSDK381:org.eclipse.osgi:3.8.1.v20120830-144521"
    }

    repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://nexus.opendaylight.org/content/groups/public/" }
        maven { url "http://nexus.opendaylight.org/content/repositories/opendaylight.release/" }
    }
}

// Utility methods start here

def addScalaLangLevelToIdea(iml) {
    def facets = iml.asNode().component.find { it.@name == 'FacetManager' } ?:
        iml.asNode().appendNode('component', [name: 'FacetManager'])
    def scala = facets.facet.find { it.@type == 'scala' } ?:
        facets.appendNode('facet', [type: 'scala', name: 'Scala'])
    def config = scala.configuration ? scala.configuration[0] :
                                       scala.appendNode('configuration', [])
    config.option.find { it.@name == 'languageLevel' } ?:
        config.appendNode('option', ['name': 'languageLevel',
                                     'value': scalaLangLevel])
}

def reorderIdeaJdkBootstrapDependency(iml) {
    def manager = iml.asNode().component.find { it.@name == 'NewModuleRootManager' }
    if (manager) {
        def jdk = manager.orderEntry.find { it.@type == 'inheritedJdk' }
        if (jdk) {
            def bootstrap = manager.orderEntry.find {
                it.@type == 'module' && it.@'module-name' == 'midonet-jdk-bootstrap' }
            if (bootstrap) {
                def i = 0
                def pos = 0
                manager.children().each {
                    if (it == jdk)
                        pos = i
                    i++;
                }
                if (pos != 0) {
                    if (manager.remove(bootstrap))
                        manager.children().add(pos, bootstrap)
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    description 'Generate wrapper, which is distributed as part of source to alleviate the need of installing gradle'
    gradleVersion = '1.12'
    distributionUrl = "https://github.com/10QueensRoad/gradle/releases/download/REL_${gradleVersion}-java8/gradle-${gradleVersion}-bin.zip"
}
