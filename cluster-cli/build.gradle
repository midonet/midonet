/*
 * Copyright 2015 Midokura SARL
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'application'

jar {
    manifest {
        attributes 'Implementation-Title': 'MidoNet Cluster CLI',
                   'Implementation-Version': version,
                   'Main-Class': 'org.midonet.cluster.cli.ClusterCli'
    }
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala', 'src/main/java'] }
        java { srcDirs = [] }
    }
    test {
        scala { srcDirs = ['src/test/scala', 'src/test/java'] }
        java { srcDirs = [] }
    }
}

dependencies {
    compile project(':cluster'),
            project(':midonet-util'),
            libraries.commons_conf,
            libraries.zookeeper,
            libraries.rx,
            libraries.protobuf,
            libraries.guava,
            libraries.jackson_mapper,
            libraries.curator_framework,
            libraries.curator_recipes,
            libraries.infinispan
    compile libraries.scala
    testCompile libraries.scalatest
}

task runClusterCli(type: JavaExec) {
    description = 'Run the Cluster Client'
    classpath sourceSets.main.runtimeClasspath
    main = "org.midonet.cluster.cli.ClusterCli"
    if (project.hasProperty("conf")) {
        args = [conf]
    } else {
        args = ["conf/cluster-cli.conf"]
    }
}